# SPDX-License-Identifier: MIT

{.used.}

import std/[importutils, sets, unittest]

import chronos, results
import libp2p/[builders, crypto/crypto, crypto/secp, switch]
import libp2p_mix

import libp2p_mix_transport

privateAccess(MixTransport)

proc createMixProtocol(): MixProtocol =
  let
    rng = newRng()
    nodeInfo = MixNodeInfo.generateRandom(4242, rng)
    privateKey = PrivateKey(scheme: Secp256k1, skkey: nodeInfo.libp2pPrivKey)
    switch = SwitchBuilder
      .new()
      .withRng(rng)
      .withPrivateKey(privateKey)
      .withAddress(nodeInfo.multiAddr)
      .withTcpTransport()
      .withMplex()
      .withNoise()
      .build()

  MixProtocol.new(nodeInfo, switch)

suite "MixTransport lifecycle":
  test "session event handlers can be registered once and removed":
    let transport = newMixTransport(createMixProtocol())
    let handler: SessionEventHandler = proc(
        event: SessionEvent
    ): Future[void] {.async: (raises: [CancelledError]).} =
      discard event

    transport.addSessionEventHandler(handler)
    transport.addSessionEventHandler(handler)
    check transport.sessionEventHandlers.len == 1

    transport.removeSessionEventHandler(handler)
    check transport.sessionEventHandlers.len == 0

  test "Data retransmissions are enabled by default and can be disabled":
    let
      mix = createMixProtocol()
      defaultTransport = newMixTransport(mix)
      retransmissionsDisabled = newMixTransport(mix, enableDataRetransmissions = false)

    check:
      defaultTransport.dataRetransmissionsEnabled
      not retransmissionsDisabled.dataRetransmissionsEnabled

  test "status probe recovery has bounded configurable attempts":
    let
      mix = createMixProtocol()
      defaultTransport = newMixTransport(mix)
      configuredTransport = newMixTransport(
        mix,
        reverseActivityTimeout = 1.minutes,
        surbStatusProbeRetryInterval = 5.seconds,
        maxSurbStatusProbeAttempts = 5,
      )

    check:
      defaultTransport.reverseActivityTimeout == DefaultReverseActivityTimeout
      defaultTransport.surbStatusProbeRetryInterval ==
        DefaultSurbStatusProbeRetryInterval
      defaultTransport.maxSurbStatusProbeAttempts == DefaultMaxSurbStatusProbeAttempts
      configuredTransport.reverseActivityTimeout == 1.minutes
      configuredTransport.surbStatusProbeRetryInterval == 5.seconds
      configuredTransport.maxSurbStatusProbeAttempts == 5

  test "SURB replenishment uses a configurable low watermark":
    let
      mix = createMixProtocol()
      defaultTransport = newMixTransport(mix)
      configuredTransport = newMixTransport(mix, surbReplenishmentLowWatermark = 4)

    check:
      DefaultSurbReplenishmentLowWatermark ==
        DefaultRecipientSurbCapacity - MaxSurbSupplyPerFrame
      defaultTransport.surbReplenishmentLowWatermark ==
        DefaultSurbReplenishmentLowWatermark
      configuredTransport.surbReplenishmentLowWatermark == 4

  test "start and stop own the Mix plug-in registrations":
    let
      mix = createMixProtocol()
      first = newMixTransport(mix)
      second = newMixTransport(mix)

    # The first transport acquires both Mix plug-in registrations.
    check waitFor(first.start()).isOk

    # Starting an already started transport is idempotent.
    check waitFor(first.start()).isOk

    # Another transport cannot acquire the same registrations.
    check waitFor(second.start()).isErr

    # Stopping the owner releases both registrations for another transport.
    waitFor(first.stop())
    check waitFor(second.start()).isOk

    waitFor(second.stop())

    # Stopping an already stopped transport is idempotent.
    waitFor(second.stop())

  test "failed start rolls back the service registration":
    let
      mix = createMixProtocol()
      transport = newMixTransport(mix)

    # Occupy the raw SURB reply handler slot. Transport startup will register
    # its service handler first and then fail to register its SURB handler.
    let surbReplyHandler: RawSurbReplyHandler = proc(
        reply: RawSurbReply
    ): Future[RawSurbReplyDisposition] {.async: (raises: [CancelledError]).} =
      discard reply
      return RawSurbReplyDisposition.Unhandled

    mix.registerRawSurbReplyHandler(surbReplyHandler).expect(
      "could not install SURB reply handler"
    )

    check waitFor(transport.start()).isErr

    # After freeing the SURB handler slot, a replacement can start only if the
    # failed startup rolled its already-registered service handler back.
    mix.unregisterRawSurbReplyHandler()

    let replacement = newMixTransport(mix)
    check waitFor(replacement.start()).isOk
    waitFor(replacement.stop())
