# SPDX-License-Identifier: MIT

{.push raises: [].}

import chronicles, chronos, results, sets, tables
import libp2p/multistream
import libp2p/peerid
import libp2p/peerstore
import libp2p/crypto/crypto
import libp2p/protocols/protocol
import libp2p/stream/bufferstream
import libp2p/stream/connection
import libp2p/utils/opt
import libp2p_mix
import libp2p_mix/[pool, multiaddr]
import ./address/parse
import ./[connect_attempts, reply_credentials, sessions, streams, trace, wire]

logScope:
  topics = "mix-transport transport"

const
  DefaultConnectTimeout* = 30.seconds
  DefaultStreamOpenTimeout* = 30.seconds
  DefaultDataRetransmissionTimeout* = 30.seconds
  DefaultSurbSupplyRetransmissionTimeout* = 30.seconds
  DefaultReverseActivityTimeout* = 2.minutes
  DefaultSurbStatusProbeRetryInterval* = 30.seconds
  DefaultMaxSurbStatusProbeAttempts* = 3
  DefaultSurbReplenishmentLowWatermark* =
    DefaultRecipientSurbCapacity - MaxSurbSupplyPerFrame
  UnknownStreamRejectionReason = "remote rejected the stream for an unknown reason"

type
  SurbSender = proc(
    surb: sink SURB, payload: sink seq[byte]
  ): Future[Result[void, string]].Raising([CancelledError]) {.gcsafe, raises: [].}

  SessionEventKind* {.pure.} = enum
    Established
    Closed

  SessionEvent* = object
    kind*: SessionEventKind
    peerId*: PeerId
    sessionId*: PeerId
    role*: SessionRole

  SessionEventHandler* = proc(event: SessionEvent): Future[void] {.
    gcsafe, async: (raises: [CancelledError])
  .}

  MixTransport* = ref object
    mix: MixProtocol
    addressDestinations: Table[PeerId, MixPubInfo]
    surbSender: SurbSender
    replyCredentials: ReplyCredentialStore
    sessions: SessionStore
    sessionEventHandlers: OrderedSet[SessionEventHandler]
    publishedSessionIds: HashSet[PeerId]
    connectTimeout: Duration
    streamOpenTimeout: Duration
    connectAttempts: ConnectAttemptCoordinator[PeerId, TransportSession]
    dataRetransmissionTimeout: Duration
    surbSupplyRetransmissionTimeout: Duration
    reverseActivityTimeout: Duration
    surbStatusProbeRetryInterval: Duration
    maxSurbStatusProbeAttempts: int
    surbReplenishmentLowWatermark: int
    dataRetransmissionsEnabled: bool
    started: bool

type PreparedReplySurbs = object
  encoded: seq[seq[byte]]
  credentials: seq[ReplyCredential]

proc handleData(self: MixTransport, frame: MixTransportFrame) {.gcsafe, raises: [].}
proc handleAcknowledgement(
  self: MixTransport, frame: MixTransportFrame
) {.gcsafe, raises: [].}

proc handleTeardownFrame(
  self: MixTransport, frame: MixTransportFrame
): Future[void] {.async: (raises: [CancelledError]).}

proc startSurbSupplier(
  self: MixTransport, session: TransportSession
) {.gcsafe, raises: [].}

proc addSessionEventHandler*(
    self: MixTransport, handler: SessionEventHandler
) {.raises: [].} =
  ## Register a handler for established and closed MixTransport sessions.
  if not handler.isNil:
    self.sessionEventHandlers.incl(handler)

proc removeSessionEventHandler*(
    self: MixTransport, handler: SessionEventHandler
) {.raises: [].} =
  self.sessionEventHandlers.excl(handler)

proc publishSessionEvent(
    self: MixTransport, session: TransportSession, kind: SessionEventKind
) {.async: (raises: [CancelledError]).} =
  case kind
  of SessionEventKind.Established:
    if session.sessionId in self.publishedSessionIds:
      return
    self.publishedSessionIds.incl(session.sessionId)
  of SessionEventKind.Closed:
    if session.sessionId notin self.publishedSessionIds:
      return
    self.publishedSessionIds.excl(session.sessionId)

  let event = SessionEvent(
    kind: kind, peerId: session.peerId, sessionId: session.sessionId, role: session.role
  )
  var handlers = newSeqOfCap[Future[void]](self.sessionEventHandlers.len)
  for handler in self.sessionEventHandlers:
    handlers.add(handler(event))
  if handlers.len > 0:
    checkFutures(await allFinished(handlers))

proc removeAndShutdownSession(
    self: MixTransport, session: TransportSession
) {.async: (raises: [CancelledError]).} =
  self.addressDestinations.del(session.sessionId)
  discard self.sessions.remove(session.sessionId)
  discard self.replyCredentials.removeSession(session.sessionId)
  await session.shutdown()
  await self.publishSessionEvent(session, SessionEventKind.Closed)

proc runProtocolHandler(
    session: TransportSession, stream: TransportStream, protocol: LPProtocol
) {.async: (raises: [CancelledError]).} =
  defer:
    stream.clearHandlerTask()
    await noCancel stream.close()
    await noCancel stream.cancelAndWaitForStreamTasks()
    protocol.releaseIncoming(stream.peerId)
    discard session.removeStream(stream.streamId)

  # Binding the template accessor preserves LPProtoHandler's raises list.
  let handler: LPProtoHandler = protocol.handler
  await handler(stream, stream.codec)

proc attachSurbSupplySnapshot(session: TransportSession, frame: var MixTransportFrame) =
  let snapshot = session.surbSupplySnapshot()
  frame.surbSupplyReceiveBase = Opt.some(snapshot.receiveBase)
  frame.surbSupplyAcknowledgementBitmap = Opt.some(snapshot.acknowledgementBitmap)
  frame.surbSupplyLimit = Opt.some(snapshot.supplyLimit)

proc applySurbSupplySnapshot(
    session: TransportSession, frame: MixTransportFrame
): bool =
  if frame.surbSupplyReceiveBase.isNone:
    return true
  session.applySurbSupplySnapshot(
    SurbSupplySnapshot(
      receiveBase: frame.surbSupplyReceiveBase.get(),
      acknowledgementBitmap: frame.surbSupplyAcknowledgementBitmap.get(),
      supplyLimit: frame.surbSupplyLimit.get(),
    )
  )

proc handleReplyFrame(
    self: MixTransport, frame: MixTransportFrame
): Future[void] {.async: (raises: [CancelledError]).} =
  logScope:
    sessionId = frame.sessionId
    kind = frame.kind

  traceInbound(frame)

  let session = self.sessions.get(frame.sessionId).valueOr:
    trace "discard reply frame - unknown session"
    return

  if not session.applySurbSupplySnapshot(frame):
    return
  if frame.surbSupplyReceiveBase.isSome:
    session.noteReverseActivity(self.reverseActivityTimeout)

  case frame.kind
  of FrameKind.ConnectAck:
    if session.role != SessionRole.Initiator:
      trace "discard reply frame - we are not the initiator"
      return
    if session.state != SessionState.Pending:
      trace "discard reply frame - not in Pending state"
      return
    session.establish()
    self.startSurbSupplier(session)
  of FrameKind.StreamAck, FrameKind.StreamReject:
    if session.state != SessionState.Established:
      trace "discard reply frame - session not established"
      return
    let stream = session.getStream(frame.streamId.get()).valueOr:
      trace "discard reply frame - unknown stream id", streamId = frame.streamId.get()
      return
    if stream.direction != StreamDirection.Outbound or
        stream.state != StreamState.Pending:
      return
    if frame.kind == FrameKind.StreamAck:
      stream.establish()
    else:
      let rejectionReason = frame.rejectionReason.get("")
      stream.reject(
        if rejectionReason.len == 0: UnknownStreamRejectionReason else: rejectionReason
      )
  of FrameKind.Data:
    self.handleData(frame)
  of FrameKind.Ack:
    self.handleAcknowledgement(frame)
  of FrameKind.SurbStatus:
    discard
  of FrameKind.CloseStream, FrameKind.ResetStream, FrameKind.Disconnect,
      FrameKind.ResetSession:
    await self.handleTeardownFrame(frame)
  else:
    discard

proc sendWithSurbRedundancyBatch(
    self: MixTransport, surbs: sink seq[SURB], payload: sink seq[byte]
): Future[Result[void, string]] {.async: (raises: [CancelledError]).} =
  var sent = false
  for surb in surbs.mitems:
    # Each SURB is consumed once, but every redundant packet needs the same
    # payload. Passing payload without move lets Nim copy it for each send.
    traceOutbound(surb, payload)
    if (await self.surbSender(move(surb), payload)).isOk:
      sent = true

  if not sent:
    return err("could not send through any SURB in the redundancy batch")
  ok()

proc waitForReplySurbs(
    session: TransportSession, count: int
): Future[Result[void, string]] {.async: (raises: [CancelledError]).} =
  while session.receivedSurbCount < count:
    if session.state != SessionState.Established:
      return err("session closed while waiting for reply SURBs")
    session.clearReplyCapacityStateChanged()
    if session.receivedSurbCount < count:
      await session.waitForReplyCapacityStateChange()
  ok()

proc sendToDestination(
    self: MixTransport, destination: PeerId, sessionId: PeerId, payload: seq[byte]
): Future[Result[void, string]].Raising([CancelledError]) {.gcsafe, raises: [].} =
  if sessionId notin self.addressDestinations:
    return
      self.mix.send(MixDestination.exitNode(destination), MixTransportCodec, payload)
  # MixNodePool reads these three entries from the switch's peer store. Override
  # only this destination, including its preferred address, for route creation.
  # pool.add/remove would leave address/key changes behind and might select a
  # previously observed address instead of the supplied candidate.
  template temporarilySet(bookType, value: untyped) =
    let book = self.mix.switch.peerStore[bookType]
    let existed = destination in book.book
    let previous = book.book.getOrDefault(destination)
    # Do not publish discovery events for these temporary routing entries:
    # change handlers could start unrelated flows while the entry is present.
    book.book[destination] = value
    defer:
      if existed:
        book.book[destination] = previous
      else:
        book.book.del(destination)

  let info = self.addressDestinations.getOrDefault(sessionId)
  temporarilySet(MixPubKeyBook, info.mixPubKey)
  temporarilySet(KeyBook, PublicKey(scheme: Secp256k1, skkey: info.libp2pPubKey))
  temporarilySet(LastSeenOutboundBook, Opt.some(info.multiAddr))
  # Chronos starts send eagerly; Mix finishes route construction before its
  # first await (sendPacket). All entries are restored before this future is
  # returned, so no unrelated flow can select the temporary peer as a relay.
  self.mix.send(MixDestination.exitNode(destination), MixTransportCodec, payload)

  # FIXME this whole thing is a brittle hack. Ideally Mix should provide a
  #   proper API for sending to a destination without requiring insertion into
  #   MixNodePool.

proc sendStreamFrame(
    self: MixTransport, session: TransportSession, frame: MixTransportFrame
): Future[Result[void, string]] {.async: (raises: [CancelledError]).} =
  trace "sending stream frame",
    frameKind = frame.kind, sessionId = session.sessionId, role = session.role

  case session.role
  of SessionRole.Initiator:
    let payload = frame.encode().valueOr:
      return err("could not encode " & $frame.kind & " frame: " & error)

    traceOutbound(frame)
    let destination = session.destination.valueOr:
      return err("initiator session has no destination")
    (await self.sendToDestination(destination, session.sessionId, payload)).isOkOr:
      return err("could not send " & $frame.kind & " frame: " & error)
  of SessionRole.Recipient:
    await session.acquireReplySend()
    defer:
      session.releaseReplySend()
    (await session.waitForReplySurbs(DefaultReplySurbRedundancy)).isOkOr:
      return err(error)
    var replyBatch = session.takeReceivedSurbs(DefaultReplySurbRedundancy).valueOr:
      return err(error)
    var replyFrame = frame
    session.attachSurbSupplySnapshot(replyFrame)
    let payload = replyFrame.encode().valueOr:
      return err("could not encode " & $frame.kind & " frame: " & error)
    (await self.sendWithSurbRedundancyBatch(replyBatch, payload)).isOkOr:
      return err("could not send " & $frame.kind & " frame: " & error)
  ok()

proc sendTeardownFrame(
    self: MixTransport, session: TransportSession, frame: MixTransportFrame
): Future[bool] {.async: (raises: []).} =
  # Teardown is best effort. In particular, a recipient must not wait forever
  # for reply SURBs merely to report that its local stream has already closed.
  case session.role
  of SessionRole.Initiator:
    let payload = frame.encode().valueOr:
      return false
    let destination = session.destination.valueOr:
      return false
    return (
      await noCancel self.sendToDestination(destination, session.sessionId, payload)
    ).isOk
  of SessionRole.Recipient:
    if session.receivedSurbCount < DefaultReplySurbRedundancy:
      return false
    await noCancel session.acquireReplySend()
    defer:
      session.releaseReplySend()
    if session.receivedSurbCount < DefaultReplySurbRedundancy:
      return false
    var replyBatch = session.takeReceivedSurbs(DefaultReplySurbRedundancy).valueOr:
      return false
    var replyFrame = frame
    session.attachSurbSupplySnapshot(replyFrame)
    let payload = replyFrame.encode().valueOr:
      return false
    return (await noCancel self.sendWithSurbRedundancyBatch(replyBatch, payload)).isOk

proc writeStream(
    self: MixTransport,
    session: TransportSession,
    stream: TransportStream,
    data: sink seq[byte],
): Future[void] {.async: (raises: [CancelledError, LPStreamError]).} =
  if stream.state != StreamState.Established:
    raise newException(LPStreamError, "MixTransport stream is not established")

  var offset = 0
  while offset < data.len:
    await stream.waitForOutboundCapacity()
    let chunkLength = min(MaxDataPayloadBytes, data.len - offset)
    var chunk = data[offset ..< offset + chunkLength]
    let reservedSequence = stream.reserveOutbound(chunk).valueOr:
      raise newException(LPStreamError, error)
    let frame = MixTransportFrame(
      version: MixTransportVersion,
      sessionId: session.sessionId,
      kind: FrameKind.Data,
      streamId: Opt.some(stream.streamId),
      sequence: Opt.some(reservedSequence),
      payload: Opt.some(move(chunk)),
    )
    (await self.sendStreamFrame(session, frame)).isOkOr:
      stream.cancelOutbound(reservedSequence)
      raise newException(LPStreamError, error)
    if self.dataRetransmissionsEnabled:
      stream.scheduleOutboundRetransmission(
        reservedSequence, self.dataRetransmissionTimeout
      )
    offset += chunkLength
    stream.activity = true

proc finishRemoteSession(
    self: MixTransport, session: TransportSession
): Future[void] {.async: (raises: [CancelledError]).} =
  await self.removeAndShutdownSession(session)

proc finishRemoteStream(
    self: MixTransport, session: TransportSession, stream: TransportStream
): Future[void] {.async: (raises: []).} =
  stream.suppressRemoteTeardown()
  discard session.removeStream(stream.streamId)
  await stream.close()
  if session.remoteDisconnectRequested and session.streamCount == 0:
    await noCancel self.finishRemoteSession(session)

proc runInboundDelivery(
    self: MixTransport, session: TransportSession, stream: TransportStream
) {.async: (raises: [CancelledError]).} =
  while not stream.closed:
    stream.clearInboundDataAvailable()
    while true:
      var inbound = stream.takeNextInbound().valueOr:
        break
      try:
        await stream.pushData(move(inbound.payload))
      except LPStreamError:
        return
      stream.advanceReceiveWindow(inbound.sequence)
      if stream.remoteCloseReady:
        await noCancel self.finishRemoteStream(session, stream)
        return
    await stream.waitForInboundData()

proc runAcknowledgements(
    self: MixTransport, session: TransportSession, stream: TransportStream
) {.async: (raises: [CancelledError]).} =
  while not stream.closed:
    await stream.waitForShouldSendAck()
    if stream.closed:
      return
    stream.clearShouldSendAck()
    let snapshot = stream.acknowledgementSnapshot()

    let frame = MixTransportFrame(
      version: MixTransportVersion,
      sessionId: session.sessionId,
      kind: FrameKind.Ack,
      streamId: Opt.some(stream.streamId),
      receiveBase: Opt.some(snapshot.receiveBase),
      acknowledgementBitmap: Opt.some(snapshot.acknowledgementBitmap),
    )
    if (await self.sendStreamFrame(session, frame)).isErr:
      return

proc runRetransmissions(
    self: MixTransport, session: TransportSession, stream: TransportStream
) {.async: (raises: [CancelledError]).} =
  while not stream.closed:
    stream.clearRetransmissionStateChanged()

    var retransmission = stream.takeDueOutboundRetransmission().valueOr:
      let deadline = stream.earliestRetransmissionDeadline().valueOr:
        await stream.waitForRetransmissionStateChange()
        continue
      let waitTime = deadline - Moment.now()
      if waitTime > ZeroDuration:
        discard await stream.waitForRetransmissionStateChange().withTimeout(waitTime)
      continue

    let frame = MixTransportFrame(
      version: MixTransportVersion,
      sessionId: session.sessionId,
      kind: FrameKind.Data,
      streamId: Opt.some(stream.streamId),
      sequence: Opt.some(retransmission.sequence),
      payload: Opt.some(move(retransmission.payload)),
    )
    let sent = await self.sendStreamFrame(session, frame)
    if sent.isErr:
      debug "Could not retransmit Data frame",
        sessionId = session.sessionId,
        streamId = stream.streamId,
        sequence = retransmission.sequence,
        error = sent.error
    stream.scheduleOutboundRetransmission(
      retransmission.sequence, self.dataRetransmissionTimeout
    )

proc configureStream(
    self: MixTransport, session: TransportSession, stream: TransportStream
) =
  let writeHandler: StreamWriteHandler = proc(
      data: sink seq[byte]
  ): Future[void] {.async: (raw: true, raises: [CancelledError, LPStreamError]).} =
    self.writeStream(session, stream, move(data))
  stream.setWriteHandler(writeHandler)
  let teardownHandler: StreamTeardownHandler = proc(
      reset: bool, finalSequence: SequenceNumber
  ): Future[void] {.async: (raises: []).} =
    let frame = MixTransportFrame(
      version: MixTransportVersion,
      sessionId: session.sessionId,
      kind: if reset: FrameKind.ResetStream else: FrameKind.CloseStream,
      streamId: Opt.some(stream.streamId),
      finalSequence:
        if reset:
          Opt.none(SequenceNumber)
        else:
          Opt.some(finalSequence),
    )
    discard await self.sendTeardownFrame(session, frame)
    await stream.cancelAndWaitForStreamTasks()
    discard session.removeStream(stream.streamId)
    if session.remoteDisconnectRequested and session.streamCount == 0:
      await noCancel self.finishRemoteSession(session)
  stream.setTeardownHandler(teardownHandler)
  stream.trackStreamTask(runInboundDelivery(self, session, stream))
  stream.trackStreamTask(runAcknowledgements(self, session, stream))
  if self.dataRetransmissionsEnabled:
    stream.trackStreamTask(runRetransmissions(self, session, stream))

proc handleData(self: MixTransport, frame: MixTransportFrame) {.gcsafe, raises: [].} =
  logScope:
    sessionId = frame.sessionId
    frameKind = frame.kind

  let session = self.sessions.get(frame.sessionId).valueOr:
    debug "discarding frame: session not found"
    return
  if session.state != SessionState.Established:
    debug "discarding frame: session not yet established"
    return
  let stream = session.getStream(frame.streamId.get()).valueOr:
    debug "discarding frame: stream not found"
    return
  if stream.state != StreamState.Established:
    debug "discarding frame: stream not yet established"
    return
  discard stream.receiveData(frame.sequence.get(), frame.payload.get())

proc handleAcknowledgement(
    self: MixTransport, frame: MixTransportFrame
) {.gcsafe, raises: [].} =
  let session = self.sessions.get(frame.sessionId).valueOr:
    debug "Dropping Ack frame for unknown session", sessionId = frame.sessionId
    return
  if session.state != SessionState.Established:
    debug "Dropping Ack frame because session is not established",
      sessionId = frame.sessionId, sessionState = session.state
    return
  let stream = session.getStream(frame.streamId.get()).valueOr:
    debug "Dropping Ack frame for unknown stream",
      sessionId = frame.sessionId, streamId = frame.streamId.get()
    return
  if stream.state != StreamState.Established:
    debug "Dropping Ack frame because stream is not established",
      sessionId = frame.sessionId,
      streamId = stream.streamId,
      streamState = stream.state
    return
  discard stream.applyAcknowledgement(
    frame.receiveBase.get(), frame.acknowledgementBitmap.get()
  )

proc handleTeardownFrame(
    self: MixTransport, frame: MixTransportFrame
): Future[void] {.async: (raises: [CancelledError]).} =
  let session = self.sessions.get(frame.sessionId).valueOr:
    debug "Dropping teardown frame for unknown session",
      sessionId = frame.sessionId, frameKind = frame.kind
    return

  case frame.kind
  of FrameKind.CloseStream, FrameKind.ResetStream:
    let stream = session.getStream(frame.streamId.get()).valueOr:
      debug "Dropping teardown frame for unknown stream",
        sessionId = frame.sessionId,
        streamId = frame.streamId.get(),
        frameKind = frame.kind
      return
    if frame.kind == FrameKind.ResetStream:
      stream.receiveRemoteReset()
      await noCancel self.finishRemoteStream(session, stream)
      return
    let ready = stream.receiveRemoteClose(frame.finalSequence.get()).valueOr:
      debug "Dropping invalid CloseStream frame",
        sessionId = frame.sessionId, streamId = frame.streamId.get(), error
      return
    if ready:
      await noCancel self.finishRemoteStream(session, stream)
  of FrameKind.Disconnect:
    session.requestRemoteDisconnect()
    if session.streamCount == 0:
      await noCancel self.finishRemoteSession(session)
  of FrameKind.ResetSession:
    session.receiveRemoteReset()
    await noCancel self.finishRemoteSession(session)
  else:
    discard

proc handleSurbSupply(
    self: MixTransport, frame: MixTransportFrame
) {.gcsafe, raises: [].} =
  let session = self.sessions.get(frame.sessionId).valueOr:
    debug "Dropping SurbSupply frame for unknown session", sessionId = frame.sessionId
    return
  if session.role != SessionRole.Recipient:
    debug "Dropping SurbSupply frame for session with unexpected role",
      sessionId = frame.sessionId, sessionRole = session.role
    return
  if session.state != SessionState.Established:
    debug "Dropping SurbSupply frame because session is not established",
      sessionId = frame.sessionId, sessionState = session.state
    return

  let firstSequence = frame.firstSurbSequence.get()
  for index, encodedSurb in frame.surbs:
    let sequence = firstSequence + SurbSupplySequence(index)
    let surb = encodedSurb.deserializeSurb().valueOr:
      continue
    discard session.acceptSurbSupply(sequence, surb)

proc sendStreamResponse(
    self: MixTransport,
    session: TransportSession,
    replyBatch: sink seq[SURB],
    streamId: StreamId,
    kind: FrameKind,
    rejectionReason = "",
): Future[bool] {.async: (raises: [CancelledError]).} =
  doAssert kind in {FrameKind.StreamAck, FrameKind.StreamReject}
  doAssert (kind == FrameKind.StreamReject) == (rejectionReason.len > 0)

  logScope:
    sessionId = session.sessionId
    streamId = streamId
    kind = kind

  let boundedRejectionReason =
    if rejectionReason.len > MaxStreamRejectionReasonBytes:
      rejectionReason[0 ..< MaxStreamRejectionReasonBytes]
    else:
      rejectionReason

  trace "sending stream response", rejectionReason = boundedRejectionReason
  var response = MixTransportFrame(
    version: MixTransportVersion,
    sessionId: session.sessionId,
    kind: kind,
    streamId: Opt.some(streamId),
    rejectionReason:
      if kind == FrameKind.StreamReject:
        Opt.some(boundedRejectionReason)
      else:
        Opt.none(string),
  )
  session.attachSurbSupplySnapshot(response)
  let payload = response.encode().valueOr:
    return false
  (await self.sendWithSurbRedundancyBatch(replyBatch, payload)).isOk

proc handleConnect(
    self: MixTransport, frame: MixTransportFrame
): Future[void] {.async: (raises: [CancelledError]).} =
  logScope:
    sessionId = frame.sessionId

  if self.sessions.get(frame.sessionId).isSome:
    trace "session already exists, ignoring connect"
    return

  var replyBatch = newSeqOfCap[SURB](DefaultReplySurbRedundancy)
  for index in 0 ..< DefaultReplySurbRedundancy:
    let surb = frame.surbs[index].deserializeSurb().valueOr:
      return
    replyBatch.add(surb)
  let session = self.sessions.addRecipientSession(frame.sessionId).valueOr:
    error "error registering session", error = error
    return

  var keepSession = false
  defer:
    if not keepSession:
      discard self.sessions.remove(frame.sessionId)

  session.initializeSurbSupply().isOkOr:
    return
  if frame.surbs.len > DefaultReplySurbRedundancy:
    let firstSequence = frame.firstSurbSequence.get()
    for index in DefaultReplySurbRedundancy ..< frame.surbs.len:
      let surb = frame.surbs[index].deserializeSurb().valueOr:
        continue
      let sequence =
        firstSequence + SurbSupplySequence(index - DefaultReplySurbRedundancy)
      discard session.acceptSurbSupply(sequence, surb)
  var acknowledgement = MixTransportFrame(
    version: MixTransportVersion, sessionId: frame.sessionId, kind: FrameKind.ConnectAck
  )
  session.attachSurbSupplySnapshot(acknowledgement)
  let payload = acknowledgement.encode().valueOr:
    return

  # Marks the session as established BEFORE sending out ACKs
  # or the other side might try to use it before it's ready
  # and have its frames dropped.
  session.establish()
  (await self.sendWithSurbRedundancyBatch(replyBatch, payload)).isOkOr:
    return

  keepSession = true
  await self.publishSessionEvent(session, SessionEventKind.Established)

proc handleOpenStream(
    self: MixTransport, frame: MixTransportFrame
): Future[void] {.async: (raises: [CancelledError]).} =
  let session = self.sessions.get(frame.sessionId).valueOr:
    debug "Dropping OpenStream frame for unknown session", sessionId = frame.sessionId
    return
  if session.role != SessionRole.Recipient:
    debug "Dropping OpenStream frame for session with unexpected role",
      sessionId = frame.sessionId, sessionRole = session.role
    return
  if session.state != SessionState.Established:
    debug "Dropping OpenStream frame because session is not established",
      sessionId = frame.sessionId, sessionState = session.state
    return

  var replyBatch = newSeqOfCap[SURB](DefaultReplySurbRedundancy)
  for index in 0 ..< DefaultReplySurbRedundancy:
    let surb = frame.surbs[index].deserializeSurb().valueOr:
      return
    replyBatch.add(surb)
  if frame.surbs.len > DefaultReplySurbRedundancy:
    let firstSequence = frame.firstSurbSequence.get()
    for index in DefaultReplySurbRedundancy ..< frame.surbs.len:
      let surb = frame.surbs[index].deserializeSurb().valueOr:
        continue
      let sequence =
        firstSequence + SurbSupplySequence(index - DefaultReplySurbRedundancy)
      discard session.acceptSurbSupply(sequence, surb)

  let protocol = self.mix.switch.ms.lookupProtocol(frame.codec.get()).valueOr:
    discard await self.sendStreamResponse(
      session,
      move(replyBatch),
      frame.streamId.get(),
      FrameKind.StreamReject,
      "requested protocol is not supported",
    )
    return

  if not protocol.reserveIncoming(session.peerId):
    discard await self.sendStreamResponse(
      session,
      move(replyBatch),
      frame.streamId.get(),
      FrameKind.StreamReject,
      "requested protocol cannot accept another incoming stream",
    )
    return

  var keepReservation = false
  defer:
    if not keepReservation:
      protocol.releaseIncoming(session.peerId)

  let streamResult = session.addInboundStream(frame.streamId.get(), frame.codec.get())
  if streamResult.isErr:
    discard await self.sendStreamResponse(
      session,
      move(replyBatch),
      frame.streamId.get(),
      FrameKind.StreamReject,
      streamResult.error,
    )
    return
  let stream = streamResult.get()
  var keepStream = false
  defer:
    if not keepStream:
      discard session.removeStream(stream.streamId)
      await noCancel stream.shutdown()

  # Install the stream's Data delivery, acknowledgement, retransmission and
  # teardown machinery before the first StreamAck can reach the initiator.
  # The initiator may start using the stream as soon as that first redundant
  # acknowledgement arrives.
  self.configureStream(session, stream)
  stream.establish()
  if not await self.sendStreamResponse(
    session, move(replyBatch), stream.streamId, FrameKind.StreamAck
  ):
    return

  let handlerTask = runProtocolHandler(session, stream, protocol)
  # If the handler dies immediately, don't set it: the cleanup in
  # runProtocolHandler has already run, and will fail to clear it.
  if not handlerTask.finished:
    stream.setHandlerTask(handlerTask)

  keepStream = true
  keepReservation = true

proc handleSurbStatusProbe(
    self: MixTransport, frame: MixTransportFrame
): Future[void] {.async: (raises: [CancelledError]).} =
  let session = self.sessions.get(frame.sessionId).valueOr:
    return
  if session.role != SessionRole.Recipient or session.state != SessionState.Established:
    return

  var replyBatch = newSeqOfCap[SURB](DefaultReplySurbRedundancy)
  for encodedSurb in frame.surbs:
    let surb = encodedSurb.deserializeSurb().valueOr:
      continue
    replyBatch.add(surb)
    if replyBatch.len == DefaultReplySurbRedundancy:
      break
  if replyBatch.len < DefaultReplySurbRedundancy:
    return

  var response = MixTransportFrame(
    version: MixTransportVersion,
    sessionId: session.sessionId,
    kind: FrameKind.SurbStatus,
  )
  session.attachSurbSupplySnapshot(response)
  let payload = response.encode().valueOr:
    return
  discard await self.sendWithSurbRedundancyBatch(replyBatch, payload)

proc handleDelivery(
    self: MixTransport, delivery: MixDelivery
): Future[void] {.async: (raises: [CancelledError]).} =
  let frame = MixTransportFrame.decode(delivery.payload).valueOr:
    error "failed to decode mix transport frame", error = error
    return

  traceInbound(frame)

  trace "handling request transport frame",
    frameKind = frame.kind, sessionId = frame.sessionId
  case frame.kind
  of FrameKind.Connect:
    await self.handleConnect(frame)
  of FrameKind.OpenStream:
    await self.handleOpenStream(frame)
  of FrameKind.Data:
    self.handleData(frame)
  of FrameKind.Ack:
    self.handleAcknowledgement(frame)
  of FrameKind.SurbSupply:
    self.handleSurbSupply(frame)
  of FrameKind.SurbStatusProbe:
    await self.handleSurbStatusProbe(frame)
  of FrameKind.CloseStream, FrameKind.ResetStream, FrameKind.Disconnect,
      FrameKind.ResetSession:
    await self.handleTeardownFrame(frame)
  else:
    discard

proc handleRawSurbReply(
    self: MixTransport, reply: RawSurbReply
): Future[RawSurbReplyDisposition] {.async: (raises: [CancelledError]).} =
  traceInbound(reply)
  if self.replyCredentials.isRetiredIdentifier(reply.identifier):
    return RawSurbReplyDisposition.Handled

  let recovered = self.replyCredentials.recoverReply(reply).valueOr:
    return RawSurbReplyDisposition.Handled

  recovered.withValue(value):
    let frame = MixTransportFrame.decode(value.payload).valueOr:
      return RawSurbReplyDisposition.Handled
    if frame.sessionId == value.sessionId:
      await self.handleReplyFrame(frame)
    return RawSurbReplyDisposition.Handled

  RawSurbReplyDisposition.Unhandled

proc newMixTransport*(
    mix: MixProtocol,
    connectTimeout = DefaultConnectTimeout,
    streamOpenTimeout = DefaultStreamOpenTimeout,
    dataRetransmissionTimeout = DefaultDataRetransmissionTimeout,
    surbSupplyRetransmissionTimeout = DefaultSurbSupplyRetransmissionTimeout,
    reverseActivityTimeout = DefaultReverseActivityTimeout,
    surbStatusProbeRetryInterval = DefaultSurbStatusProbeRetryInterval,
    maxSurbStatusProbeAttempts = DefaultMaxSurbStatusProbeAttempts,
    surbReplenishmentLowWatermark = DefaultSurbReplenishmentLowWatermark,
    enableDataRetransmissions = true,
    recipientSurbCapacity = DefaultRecipientSurbCapacity,
): MixTransport =
  doAssert not mix.isNil, "MixProtocol must not be nil"
  doAssert connectTimeout > ZeroDuration, "connect timeout must be positive"
  doAssert streamOpenTimeout > ZeroDuration, "stream open timeout must be positive"
  doAssert dataRetransmissionTimeout > ZeroDuration,
    "Data retransmission timeout must be positive"
  doAssert surbSupplyRetransmissionTimeout > ZeroDuration,
    "SURB supply retransmission timeout must be positive"
  doAssert reverseActivityTimeout > ZeroDuration,
    "reverse activity timeout must be positive"
  doAssert surbStatusProbeRetryInterval > ZeroDuration,
    "SURB status probe retry interval must be positive"
  doAssert maxSurbStatusProbeAttempts > 0,
    "maximum SURB status probe attempts must be positive"
  doAssert surbReplenishmentLowWatermark >= 0,
    "SURB replenishment low watermark must not be negative"
  doAssert surbReplenishmentLowWatermark < recipientSurbCapacity,
    "SURB replenishment low watermark must be below recipient capacity"
  doAssert recipientSurbCapacity >= MaxConnectSurbs - DefaultReplySurbRedundancy,
    "recipient SURB capacity must hold the Connect bootstrap supply"
  let surbSender: SurbSender = proc(
      surb: sink SURB, payload: sink seq[byte]
  ): Future[Result[void, string]] {.async: (raw: true, raises: [CancelledError]).} =
    mix.sendWithSurb(move(surb), move(payload))

  MixTransport(
    mix: mix,
    surbSender: surbSender,
    replyCredentials: ReplyCredentialStore.new(),
    sessions: newSessionStore(recipientSurbCapacity),
    sessionEventHandlers: initOrderedSet[SessionEventHandler](),
    publishedSessionIds: initHashSet[PeerId](),
    connectTimeout: connectTimeout,
    streamOpenTimeout: streamOpenTimeout,
    connectAttempts: newConnectAttemptCoordinator[PeerId, TransportSession](),
    dataRetransmissionTimeout: dataRetransmissionTimeout,
    surbSupplyRetransmissionTimeout: surbSupplyRetransmissionTimeout,
    reverseActivityTimeout: reverseActivityTimeout,
    surbStatusProbeRetryInterval: surbStatusProbeRetryInterval,
    maxSurbStatusProbeAttempts: maxSurbStatusProbeAttempts,
    surbReplenishmentLowWatermark: surbReplenishmentLowWatermark,
    dataRetransmissionsEnabled: enableDataRetransmissions,
  )

proc createReplySurbs(
    self: MixTransport, destination: PeerId, sessionId: PeerId, count: int
): Result[PreparedReplySurbs, string] =
  let mixDestination = MixDestination.exitNode(destination)
  var prepared = PreparedReplySurbs(
    encoded: newSeqOfCap[seq[byte]](count),
    credentials: newSeqOfCap[ReplyCredential](count),
  )

  for _ in 0 ..< count:
    var created = self.mix.createSurb(mixDestination).valueOr:
      return err("could not create SURB: " & error)
    prepared.encoded.add(created.surb.serializeSurb())
    prepared.credentials.add(created.credential)

  self.replyCredentials.add(sessionId, prepared.credentials).isOkOr:
    return err("could not register reply credentials: " & error)

  ok(prepared)

proc retireReplyCredentials(
    self: MixTransport, credentials: openArray[ReplyCredential]
) =
  self.replyCredentials.consume(credentials)

proc sendSurbSupply(
    self: MixTransport,
    session: TransportSession,
    firstSequence: SurbSupplySequence,
    encodedSurbs: seq[seq[byte]],
): Future[void] {.async: (raises: [CancelledError]).} =
  let frame = MixTransportFrame(
    version: MixTransportVersion,
    sessionId: session.sessionId,
    kind: FrameKind.SurbSupply,
    firstSurbSequence: Opt.some(firstSequence),
    surbs: encodedSurbs,
  )
  let sent = await self.sendStreamFrame(session, frame)
  if sent.isErr:
    debug "Could not send SURB supply",
      sessionId = session.sessionId, firstSequence, error = sent.error
  session.scheduleSurbSupplyRetransmission(
    firstSequence, encodedSurbs.len, self.surbSupplyRetransmissionTimeout
  )

proc createAndSendSurbSupply(
    self: MixTransport, session: TransportSession, count: int
): Future[bool] {.async: (raises: [CancelledError]).} =
  let destination = session.destination.valueOr:
    return false
  let prepared = self.createReplySurbs(destination, session.sessionId, count).valueOr:
    debug "Could not create SURB supply", sessionId = session.sessionId, error
    return false
  var credentialIdentifiers = newSeqOfCap[SURBIdentifier](prepared.credentials.len)
  for credential in prepared.credentials:
    credentialIdentifiers.add(credential.identifier)
  let firstSequence = session.registerSurbSupply(
    prepared.encoded, credentialIdentifiers
  ).valueOr:
    self.retireReplyCredentials(prepared.credentials)
    debug "Could not register SURB supply", sessionId = session.sessionId, error
    return false
  await self.sendSurbSupply(session, firstSequence, prepared.encoded)
  true

proc retransmitSurbSupply(
    self: MixTransport,
    session: TransportSession,
    sequence: SurbSupplySequence,
    encodedSurb: seq[byte],
): Future[void] {.async: (raises: [CancelledError]).} =
  await self.sendSurbSupply(session, sequence, @[encodedSurb])

proc sendSurbStatusProbe(
    self: MixTransport, session: TransportSession
): Future[void] {.async: (raises: [CancelledError]).} =
  let destination = session.destination.valueOr:
    return
  let prepared = self.createReplySurbs(
    destination, session.sessionId, DefaultReplySurbRedundancy
  ).valueOr:
    debug "Could not create SURB status probe reply paths",
      sessionId = session.sessionId, error
    return
  let probe = MixTransportFrame(
    version: MixTransportVersion,
    sessionId: session.sessionId,
    kind: FrameKind.SurbStatusProbe,
    surbs: prepared.encoded,
  )
  (await self.sendStreamFrame(session, probe)).isOkOr:
    self.retireReplyCredentials(prepared.credentials)
    return

proc runSurbSupplier(
    self: MixTransport, session: TransportSession
) {.async: (raises: [CancelledError]), gcsafe.} =
  defer:
    session.clearSurbSupplierTask()

  var replenishing = false
  session.noteReverseActivity(self.reverseActivityTimeout)
  while session.state == SessionState.Established:
    session.clearSurbSupplyStateChanged()

    let probeWait = session.timeUntilSurbStatusProbe()
    let statusProbeIsDue = probeWait.isSome and probeWait.get() <= ZeroDuration
    if statusProbeIsDue:
      if session.unansweredSurbStatusProbeCount >= self.maxSurbStatusProbeAttempts:
        error "MixTransport session did not respond to SURB status probes",
          sessionId = session.sessionId,
          attempts = session.unansweredSurbStatusProbeCount
        session.clearSurbSupplierTask()
        await self.removeAndShutdownSession(session)
        return
      session.recordSurbStatusProbeAttempt(self.surbStatusProbeRetryInterval)
      await self.sendSurbStatusProbe(session)
      continue

    if replenishing and session.availableSurbSupplySlots == 0:
      trace "Completed SURB replenishment cycle",
        sessionId = session.sessionId,
        projectedInventory = session.estimatedRemoteSurbInventory
      replenishing = false
    elif not replenishing and
        session.isSurbReplenishmentDue(self.surbReplenishmentLowWatermark):
      trace "Starting SURB replenishment cycle",
        sessionId = session.sessionId,
        projectedInventory = session.estimatedRemoteSurbInventory,
        lowWatermark = self.surbReplenishmentLowWatermark,
        availableSlots = session.availableSurbSupplySlots
      replenishing = true

    if replenishing and session.availableSurbSupplySlots > 0:
      let count = min(MaxSurbSupplyPerFrame, session.availableSurbSupplySlots)
      if await self.createAndSendSurbSupply(session, count):
        continue

    let retransmission = session.takeDueSurbSupplyRetransmission()
    if retransmission.isSome:
      let value = retransmission.get()
      discard self.replyCredentials.purgeExpired()
      if self.replyCredentials.get(value.credentialIdentifier).isNone:
        session.removePendingSurbSupply(value.sequence)
        debug "Discarding pending SURB supply without an active reply credential",
          sessionId = session.sessionId, sequence = value.sequence
        continue
      await self.retransmitSurbSupply(session, value.sequence, value.encodedSurb)
      continue

    var waitTime = Opt.none(Duration)
    session.earliestSurbSupplyRetransmission().withValue(deadline):
      waitTime = Opt.some(
        if deadline <= Moment.now():
          ZeroDuration
        else:
          deadline - Moment.now()
      )
    probeWait.withValue(value):
      if waitTime.isNone or value < waitTime.get():
        waitTime = Opt.some(value)

    waitTime.withValue(value):
      if value <= ZeroDuration:
        continue
      discard await session.waitForSurbSupplyStateChange().withTimeout(value)
      continue
    await session.waitForSurbSupplyStateChange()

proc startSurbSupplier(
    self: MixTransport, session: TransportSession
) {.gcsafe, raises: [].} =
  let task = self.runSurbSupplier(session)
  if not task.finished:
    session.setSurbSupplierTask(task)

proc createConnectFrame(
    self: MixTransport, destination: PeerId, session: TransportSession
): Result[MixTransportFrame, string] =
  var frame = MixTransportFrame(
    version: MixTransportVersion, sessionId: session.sessionId, kind: FrameKind.Connect
  )
  let surbCount = MaxConnectSurbs
  if surbCount < DefaultReplySurbRedundancy:
    return err("Connect frame cannot hold one reply redundancy batch")
  let prepared = self.createReplySurbs(destination, session.sessionId, surbCount).valueOr:
    return err("could not prepare Connect reply SURBs: " & error)
  frame.surbs = prepared.encoded

  let suppliedCount = surbCount - DefaultReplySurbRedundancy
  if suppliedCount > 0:
    var credentialIdentifiers = newSeqOfCap[SURBIdentifier](suppliedCount)
    for index in DefaultReplySurbRedundancy ..< prepared.credentials.len:
      credentialIdentifiers.add(prepared.credentials[index].identifier)
    let firstSequence = session.registerInitialSurbSupply(
      prepared.encoded.toOpenArray(DefaultReplySurbRedundancy, prepared.encoded.high),
      credentialIdentifiers,
    ).valueOr:
      return err("could not register initial SURB supply: " & error)
    frame.firstSurbSequence = Opt.some(firstSequence)

  ok(frame)

proc connectInternal(
    self: MixTransport,
    destination: PeerId,
    info: Opt[MixPubInfo] = Opt.none(MixPubInfo),
): Future[Result[TransportSession, string]] {.async: (raises: [CancelledError]).} =
  info.withValue(value):
    # The address codec is transport-independent. Enforce the underlying Mix
    # packet format's capabilities here, before installing routing information.
    discard multiAddrToBytes(destination, value.multiAddr).valueOr:
      return err("unsupported Mix destination address: " & error)
  let sessionId = PeerId.random(self.mix.switch.rng).valueOr:
    return err("could not generate session identifier: " & $error)
  let session = self.sessions.addInitiatorSession(destination, sessionId).valueOr:
    return err(error)

  info.withValue(value):
    self.addressDestinations[sessionId] = value

  var keepSession = false
  defer:
    if not keepSession:
      self.addressDestinations.del(sessionId)
      discard self.sessions.remove(sessionId)
      discard self.replyCredentials.removeSession(sessionId)

  let frame = self.createConnectFrame(destination, session).valueOr:
    return err(error)
  let payload = frame.encode().valueOr:
    return err("could not encode Connect frame: " & error)

  traceOutbound(frame)
  (await self.sendToDestination(destination, session.sessionId, payload)).isOkOr:
    return err("could not send Connect frame: " & error)

  let suppliedCount = frame.surbs.len - DefaultReplySurbRedundancy
  if suppliedCount > 0:
    session.scheduleSurbSupplyRetransmission(
      frame.firstSurbSequence.get(), suppliedCount, self.surbSupplyRetransmissionTimeout
    )

  if not await session.waitUntilEstablished().withTimeout(self.connectTimeout):
    return err("MixTransport connect timed out")
  if session.state != SessionState.Established:
    return err("MixTransport session closed while connecting")

  keepSession = true
  await self.publishSessionEvent(session, SessionEventKind.Established)
  ok(session)

proc getExisting(
    self: MixTransport, destination: PeerId
): Opt[TransportSession] {.nimcall, gcsafe.} =
  self.sessions.getByDestination(destination).withValue(existing):
    if existing.state == SessionState.Established:
      return Opt.some(existing)
  return Opt.none(TransportSession)

proc connect*(
    self: MixTransport, destination: PeerId, addrs: seq[MultiAddress]
): Future[Result[TransportSession, string]] {.async: (raises: [CancelledError]).} =
  if not self.started:
    return err("MixTransport is not started")

  var
    candidates: seq[MixPubInfo]
    lastError = "no usable mix address"
  for address in addrs:
    let mixinfo = MixPubInfo.fromMixAddress(destination, address).valueOr:
      lastError = error
      continue
    candidates.add(mixinfo)

  if addrs.len > 0 and candidates.len == 0:
    return err(lastError)

  let withMixInfo: ConnectOperation[PeerId, TransportSession] = proc(
      destination: PeerId
  ): Future[Result[TransportSession, string]] {.async: (raises: [CancelledError]).} =
    var failure: string
    for candidate in candidates:
      let attempt = await self.connectInternal(destination, Opt.some(candidate))
      if attempt.isOk:
        return attempt
      failure = attempt.error
    return err(failure)

  let paGetExisting: ExistingConnectionLookup[PeerId, TransportSession] = proc(
    p: PeerId
  ): Opt[TransportSession] =
    getExisting(self, p)

  trace "connect requested", destination
  let (session, existing) = (
    await self.connectAttempts.connect(destination, withMixInfo, paGetExisting)
  ).valueOr:
    return err(error)

  if existing and session.state == SessionState.Established:
    await self.publishSessionEvent(session, SessionEventKind.Established)

  ok(session)

proc connect*(
    self: MixTransport, destination: PeerId
): Future[Result[TransportSession, string]] {.async: (raises: [CancelledError]).} =
  if not self.started:
    return err("MixTransport is not started")

  let withPeerId: ConnectOperation[PeerId, TransportSession] = proc(
      destination: PeerId
  ): Future[Result[TransportSession, string]] {.
      async: (raw: true, raises: [CancelledError])
  .} =
    self.connectInternal(destination)

  let paGetExisting: ExistingConnectionLookup[PeerId, TransportSession] = proc(
    p: PeerId
  ): Opt[TransportSession] =
    getExisting(self, p)

  trace "connect requested", destination
  let (session, existing) = (
    await self.connectAttempts.connect(destination, withPeerId, paGetExisting)
  ).valueOr:
    return err(error)

  if existing and session.state == SessionState.Established:
    await self.publishSessionEvent(session, SessionEventKind.Established)

  ok(session)

proc dial*(
    self: MixTransport, destination: PeerId, addrs: seq[MultiAddress], codec: string
): Future[Result[TransportStream, string]] {.async: (raises: [CancelledError]).} =
  if not self.started:
    return err("MixTransport is not started")
  if codec.len == 0:
    return err("stream codec must not be empty")
  if codec.len > MaxCodecBytes:
    return err("stream codec is too long")

  let session = (await self.connect(destination, addrs)).valueOr:
    return err(error)
  let stream = session.addOutboundStream(codec).valueOr:
    return err(error)
  var keepStream = false
  defer:
    if not keepStream:
      discard session.removeStream(stream.streamId)
      await noCancel stream.shutdown()

  var frame = MixTransportFrame(
    version: MixTransportVersion,
    sessionId: session.sessionId,
    kind: FrameKind.OpenStream,
    streamId: Opt.some(stream.streamId),
    codec: Opt.some(codec),
  )
  let
    suppliedCount = min(
      MaxOpenStreamSurbs - DefaultReplySurbRedundancy, session.availableSurbSupplySlots
    )
    surbCount = DefaultReplySurbRedundancy + suppliedCount
  let prepared = self.createReplySurbs(destination, session.sessionId, surbCount).valueOr:
    return err("could not prepare OpenStream reply SURBs: " & error)
  frame.surbs = prepared.encoded

  var firstSupplySequence = Opt.none(SurbSupplySequence)
  var keepReplyCredentials = false
  defer:
    if not keepReplyCredentials:
      self.retireReplyCredentials(prepared.credentials)
      firstSupplySequence.withValue(sequence):
        session.removePendingSurbSupply(sequence, suppliedCount)

  if suppliedCount > 0:
    var credentialIdentifiers = newSeqOfCap[SURBIdentifier](suppliedCount)
    for index in DefaultReplySurbRedundancy ..< prepared.credentials.len:
      credentialIdentifiers.add(prepared.credentials[index].identifier)
    let firstSequence = session.registerSurbSupply(
      prepared.encoded.toOpenArray(DefaultReplySurbRedundancy, prepared.encoded.high),
      credentialIdentifiers,
    ).valueOr:
      return err("could not register OpenStream SURB supply: " & error)
    firstSupplySequence = Opt.some(firstSequence)
    frame.firstSurbSequence = firstSupplySequence

  let payload = frame.encode().valueOr:
    return err("could not encode OpenStream frame: " & error)

  traceOutbound(frame)
  (await self.sendToDestination(destination, session.sessionId, payload)).isOkOr:
    return err("could not send OpenStream frame: " & error)

  keepReplyCredentials = true
  firstSupplySequence.withValue(sequence):
    session.scheduleSurbSupplyRetransmission(
      sequence, suppliedCount, self.surbSupplyRetransmissionTimeout
    )

  if not await stream.waitUntilResolved().withTimeout(self.streamOpenTimeout):
    return err("MixTransport stream opening timed out")
  if stream.closed:
    return err("MixTransport stream closed while opening")
  if stream.state == StreamState.Rejected:
    return err(stream.rejectionReason)

  self.configureStream(session, stream)
  keepStream = true
  ok(stream)

proc dial*(
    self: MixTransport, destination: PeerId, codec: string
): Future[Result[TransportStream, string]] {.async: (raises: [CancelledError]).} =
  await self.dial(destination, @[], codec)

proc disconnect*(
    self: MixTransport, session: TransportSession
): Future[Result[void, string]] {.async: (raises: [CancelledError]).} =
  if not self.started:
    return err("MixTransport is not started")
  if self.sessions.get(session.sessionId).get(nil) != session:
    return err("session is not registered by this transport")
  if session.state != SessionState.Established:
    return err("session is not established")
  if session.streamCount != 0:
    return err("cannot disconnect a session while it has active streams")

  let frame = MixTransportFrame(
    version: MixTransportVersion,
    sessionId: session.sessionId,
    kind: FrameKind.Disconnect,
  )
  if not await self.sendTeardownFrame(session, frame):
    return err("could not send Disconnect frame")

  await self.removeAndShutdownSession(session)
  ok()

proc resetSession*(
    self: MixTransport, session: TransportSession
): Future[void] {.async: (raises: [CancelledError]).} =
  if self.sessions.get(session.sessionId).get(nil) != session:
    return
  let frame = MixTransportFrame(
    version: MixTransportVersion,
    sessionId: session.sessionId,
    kind: FrameKind.ResetSession,
  )
  discard await self.sendTeardownFrame(session, frame)
  await self.removeAndShutdownSession(session)

proc start*(
    self: MixTransport
): Future[Result[void, string]] {.async: (raises: [CancelledError]).} =
  if self.started:
    return ok()

  let deliveryHandler: MixDeliveryHandler = proc(
      delivery: MixDelivery
  ): Future[void] {.async: (raw: true, raises: [CancelledError]).} =
    self.handleDelivery(delivery)

  self.mix.registerMixDeliveryHandler(MixTransportCodec, deliveryHandler).isOkOr:
    return err(error)

  let rawSurbReplyHandler: RawSurbReplyHandler = proc(
      reply: RawSurbReply
  ): Future[RawSurbReplyDisposition] {.async: (raw: true, raises: [CancelledError]).} =
    self.handleRawSurbReply(reply)

  self.mix.registerRawSurbReplyHandler(rawSurbReplyHandler).isOkOr:
    self.mix.unregisterMixDeliveryHandler(MixTransportCodec)
    return err(error)

  self.started = true
  ok()

proc stop*(self: MixTransport): Future[void] {.async: (raises: [CancelledError]).} =
  if not self.started:
    return

  await self.connectAttempts.cancelAll("MixTransport stopped during connection attempt")

  let sessions = self.sessions.takeSessions()
  for session in sessions:
    let frame = MixTransportFrame(
      version: MixTransportVersion,
      sessionId: session.sessionId,
      kind: FrameKind.ResetSession,
    )
    discard await self.sendTeardownFrame(session, frame)

  self.mix.unregisterRawSurbReplyHandler()
  self.mix.unregisterMixDeliveryHandler(MixTransportCodec)
  var shutdownTasks = newSeqOfCap[Future[void]](sessions.len)
  for session in sessions:
    shutdownTasks.add(self.removeAndShutdownSession(session))
  if shutdownTasks.len > 0:
    checkFutures(await allFinished(shutdownTasks))
  self.replyCredentials.clear()
  self.started = false

{.pop.}
